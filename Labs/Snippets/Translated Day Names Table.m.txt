let
    Source = #table( type table [ DayNumber = Int64.Type ], List.Split({1..7}, 1) ),
    NextSunday  = Date.AddDays( Date.From(DateTime.LocalNow()), 
                                Value.Subtract(6, 
                                               Date.DayOfWeek( Date.From(DateTime.LocalNow()),
                                               Day.Monday)) ),
    Translations = Table.AddColumn( Source, "Translations",
        each
            [ DayOfWeek = Date.AddDays(NextSunday, [DayNumber] ),
              Translations = List.Transform(Languages[DefaultCulture], 
                                            each Date.DayOfWeekName(DayOfWeek, _ ) ),
              TranslationTable = Table.FromList( Translations, null ),
              TranslationsTranspose = Table.Transpose(TranslationTable),
              TranslationsColumns = Table.RenameColumns(
                TranslationsTranspose,
                List.Zip({ Table.ColumnNames( TranslationsTranspose ), 
                           List.Transform(Languages[Language], 
                           each "DayNameTranslations" & _ ) })
              )                
            ]
    ),
    ExpandedTranslations = Table.ExpandRecordColumn(Translations, 
                                                    "Translations", 
                                                    { "TranslationsColumns" }, 
                                                    { "TranslationsColumns" }),
    ColumnsCollection = List.Transform(Languages[Language], each "DayNameTranslations" & _ ),
    ExpandedTranslationsColumns = Table.ExpandTableColumn(ExpandedTranslations, 
                                                          "TranslationsColumns", 
                                                          ColumnsCollection, 
                                                          ColumnsCollection ),
    TypedColumnsCollection = List.Transform(ColumnsCollection, each {_, type text}),
    QueryOutput = Table.TransformColumnTypes(ExpandedTranslationsColumns, TypedColumnsCollection)
in
    QueryOutput
